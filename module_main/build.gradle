apply from: "../module_public.gradle"

android.resourcePrefix 'main_'//解决作为模块时候资源文件之间被覆盖问题
android.sourceSets {
    main {
        if (isModule.toBoolean()) {
            manifest.srcFile 'src/main/app/AndroidManifest.xml'
        } else {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            //编译库的时候 排除app文件夹中的所有Java文件
            java {
                exclude 'app/**'
            }
        }
    }
}

//****** 动态设置额外信息 ***************************************
android.defaultConfig.resValue "string", "build_time", buildTime() + "->" + android.defaultConfig.targetSdkVersion.apiLevel
android.defaultConfig.resValue "string", "build_host", hostName() + "->" + project.getName()
android.defaultConfig.resValue "string", "build_revision", revision()
//****** End ***************************************

//获取当前时间
def buildTime() {
    return new Date().format("yyyy-MM-dd HH:mm:ss")
}
//获取电脑的用户名和PC名
def hostName() {
    return System.getProperty("user.name") + "@" + InetAddress.localHost.hostName
}
//获取最后一次commit的sha值
def revision() {
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = code
    }
    return code.toString()
}

